buildscript {
  ext {
    novaVersion = 'edge'
    springBootVersion = '2.4.4'
    autoServiceVersion = '1.0-rc7'
    freefairVersion = '5.3.3.3'
  }
}

plugins {
  id 'base'
  id 'io.spring.nohttp' version '0.0.5.RELEASE'
  id 'com.palantir.graal' version '0.7.2' apply false
  id 'com.github.spotbugs' version '4.7.0' apply false
  id 'io.freefair.lombok' version "${freefairVersion}" // apply, because javadoc
  id 'io.freefair.git-version' version "${freefairVersion}"
  id 'com.github.jk1.dependency-license-report' version '1.16'
//  id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
//  id 'org.springframework.experimental.aot' version '0.9.1' apply false
  id 'org.springframework.boot' version "${springBootVersion}" apply false
  id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
//io.freefair.maven-optional
}

if (version.toString().endsWith('SNAPSHOT') || version.toString().isBlank()) {
  version = 'edge'
} else if (version.toString().startsWith('v')) {
  version = version.toString().substring(1)
}

allprojects {
  group('com.github.marcelcoding')

  repositories {
    mavenCentral {
      content {
        excludeGroup('net.getnova.framework')
      }
    }
    maven {
      url 'https://repo.spring.io/release'
      content {
        includeModule('org.springframework.experimental', 'spring-native')
        includeModule('org.springframework.experimental', 'spring-native-build')
      }
    }
//    maven { url 'https://repo.spring.io/milestone' }
//    maven { url 'https://repo.spring.io/snapshot' }
    maven {
      url 'https://raw.githubusercontent.com/getnova/maven/main'
      content {
        includeGroup('net.getnova.framework')
      }
    }
  }
}

subprojects {
  version rootProject.version

  apply plugin: 'java'
  apply plugin: 'io.freefair.lombok'
  apply plugin: 'checkstyle'
  apply plugin: 'pmd'
  apply plugin: 'com.github.spotbugs'

  sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11

  jar {
    from "${rootDir}/LICENSE"
    manifest.attributes([
      'Specification-Title'     : project.name,
      'Specification-Version'   : project.version,
      'Specification-Vendor'    : 'Marcel Coding (https://github.com/MarcelCoding)',
      'Implementation-Title'    : project.name,
      'Implementation-Version'  : project.version,
      'Implementation-Vendor'   : 'Marcel Coding (https://github.com/MarcelCoding)',
      'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ')
    ])
  }

  tasks.withType(JavaCompile) {
    doFirst {
      options.encoding = 'UTF-8'
      // Java Modularity:
      // options.compilerArgs.addAll(['--module-path', classpath.asPath])
    }
  }

  lombok {
    config.put('lombok.equalsandhashcode.callsuper', 'CALL')
    config.put('lombok.tostring.callsuper', 'CALL')
  }

  tasks.withType(Checkstyle) {
    exclude('**/module-info.java')
    ignoreFailures = true
    reports {
      xml.enabled(true)
      html.enabled(false)
    }
  }

  checkstyleMain {
    configFile = file("${rootDir}/config/checkstyle/main.xml")
  }

  checkstyleTest {
    configFile = file("${rootDir}/config/checkstyle/test.xml")
  }

  tasks.withType(Pmd) {
    ignoreFailures = true
    reports {
      xml.enabled(true)
      html.enabled(false)
    }
  }

  spotbugs {
    ignoreFailures = true
  }

  test {
    useJUnitPlatform()
  }
}
